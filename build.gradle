apply plugin: 'scala'
apply plugin: 'java'
apply plugin: 'idea'

import groovy.io.FileType
import java.util.regex.Pattern
import org.apache.tools.ant.filters.ReplaceTokens

version = "0.7"

defaultTasks 'build'
//defaultTasks 'compileScala'

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url "http://repo.bukkit.org/content/repositories/snapshots/"
  }
  maven {
    url "http://maven.clapper.org/"
  }
  maven {
    url "http://dl.bintray.com/joshcough/maven/"
  }
  maven {
    url "http://repo.md-5.net/content/repositories/snapshots"
  }

}

// See http://goo.gl/sN5GVB
configurations {
  sshAntTask
}

ext.pluginClass = "org.clapper.minecraft.EndlessDispenser.EndlessDispenserPlugin"
ext.author      = "Brian M. Clapper"

ext.mclibVersion = "0.8"
ext.home = System.properties['user.home']
ext.jc = "com.joshcough:scala-minecraft-plugin-api_2.10:0.3.2"

dependencies {
  compile "org.scala-lang:scala-compiler:2.10.2"
  // When calling anything directly on the Bukkit object, the Servlet API
  // is (often) required to avoid weird compilation errors related to a
  // stub version included in the Bukket jar.
  compile "javax.servlet:servlet-api:2.5"
  compile "org.bukkit:bukkit:1.7.9-R0.2-SNAPSHOT"
  compile "org.bukkit:craftbukkit:1.7.9-R0.2-SNAPSHOT"
/*
  compile "org.spigotmc:spigot-api:1.7.2-R0.1-SNAPSHOT"
  compile "org.spigotmc:spigot:1.7.2-R0.1-SNAPSHOT"
*/
  compile "org.clapper.minecraft:mclib:${ext.mclibVersion}"
  compile ext.jc

  runtime "org.scala-lang:scala-library:2.10.2"
  runtime(ext.jc) {
    transitive = false
  }
  runtime("com.joshcough:scala-minecraft-scala-library_2.10:0.3.2") {
    transitive = false
  }
  runtime("org.clapper.minecraft:mclib:${ext.mclibVersion}") {
    transitive = false
  }

  sshAntTask "org.apache.ant:ant-jsch:1.9.2", "com.jcraft:jsch:0.1.50"
}

// --------------------------------------------------------------------------
// Tasks
// --------------------------------------------------------------------------

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
  scalaCompileOptions.additionalParameters = ["-feature"]

  configure(scalaCompileOptions.forkOptions) {
    memoryMaximumSize = '1g'
  }
}

task zip(dependsOn: ['zipCopy', 'zipJars'])

task zipCopy(type: Copy, dependsOn: 'jar') {
  from fileTree('build/libs') {
    include "*.jar"
  }
  from configurations.runtime.files
  into 'build/zipcopy'
}

task zipCleanup(type: Delete) {
  delete 'build/zipcopy'
}

task zipJars(type: Zip) {
  archiveName "mc-endless-dispenser-${version}.zip"
  includeEmptyDirs false
  finalizedBy 'zipCleanup'

  destinationDir file("build/distributions")

  from fileTree('build/zipcopy') {
    include "mclib*.jar"
    include "scala-library*.jar"
    include "EndlessDispenser*.jar"
    include "scala-minecraft-plugin-api*.jar"
  }

  into "/mc-endless-dispenser"
}

processResources.dependsOn('createPluginYaml')

task createPluginYaml(type: Copy) {
  from("templates") {
    include "plugin.yml"
  }
  into 'src/main/resources'
  filter(ReplaceTokens, tokens: [
    version: version,
    name:    project.name,
    class:   project.ext.pluginClass,
    genTime: new Date().format("yyyy/MM/dd HH:mm:ss"),
    author:  project.ext.author
  ])
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}

clean.dependsOn('localClean')

task localClean(type: Delete) {
  delete "src/main/resources/plugin.yml"
}

// Publish the zip file to the remote server.
task publish(dependsOn: 'zip') << {

  // Get the passphrase from the user.
  def console = System.console()
  def passphrase = console.readPassword("\n%s: ", "SSH keyfile passphrase")

  // Redefine the scp Ant task, with our classpath.
  ant.taskdef(name:      'scp',
    classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
    classpath: configurations.sshAntTask.asPath)

  // Invoke the scp Ant task. (Use "gradle -i publish" to see SSH output.)
  // Assumes PUBLISH_TO and SSH_KEYFILE are set in the environment (e.g.,
  // via a direnv ".envrc" file).
  ant.scp(todir:      System.env["PUBLISH_TO"] + "/" + "mc-endless-dispenser",
    keyfile:    System.env["SSH_KEYFILE"],
    passphrase: passphrase as String,
    verbose:    'true') {
    fileset(dir: 'build/distributions') {
      include(name: '*.zip')
    }
  }
}

// --------------------------------------------------------------------------
// Task re-configurations
// --------------------------------------------------------------------------

jar.dependsOn('createPluginYaml')

jar {
  manifest {
    def manifestCP = ['../lib/mclib.jar',
                      '../lib/scala-library-2.10.2.jar',
                      'scala-minecraft-scala-library-2.10-0.3.2.jar',
                      'scala-minecraft-plugin-api_2.10-0.3.2.jar',
                      '../craftbukkit.jar']
    attributes 'Class-Path': manifestCP.join(" ")
  }
}
